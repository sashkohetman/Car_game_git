extends RayCast3D
class_name RaycastWheel

@export var shapecast : ShapeCast3D
@export var offset_shapecast : float = 0.3

@export_group("Wheel properties")
@export var spring_strenght := 40000.0
@export var spring_damping := 4000.0
@export var max_spring_force : float = INF
@export var rest_dist := 0.6
@export var over_extend := 0.05 
@export var wheel_radius := 0.2
@export var z_traction := 0.05
@export var z_brake_traction := 0.25

@export_group("Motor")
@export var is_motor := false
@export var is_steer := false
@export var grip_curve : Curve
@export var accel_curve : Curve

@export_group("Debug")
@export var show_debug := false


@onready var Wheel_model: Node3D = get_child(0)


var engine_force := 0.0
var grip_factor := 0.0
var is_braking := false


func _ready() -> void:
	target_position.y = -(rest_dist + wheel_radius + over_extend)
	
	if shapecast:
		shapecast.target_position.x = -(rest_dist + over_extend) - offset_shapecast
		shapecast.add_exception(get_parent())
		shapecast.position.y = offset_shapecast
	
func apply_wheel_physics(car: RaycastCar) -> void:
	force_raycast_update()
	if shapecast:
		shapecast.force_shapecast_update()
		
	target_position.y = -(rest_dist + wheel_radius + over_extend)
	
	var colliding := is_colliding()
	if shapecast and shapecast.is_colliding():
		colliding = true
	
	if not colliding:
		Wheel_model.position.y = move_toward(Wheel_model.position.y, -rest_dist, 5 * get_physics_process_delta_time())
		return
	
	var contact : Vector3
	var normal : Vector3
	
	if shapecast and shapecast.is_colliding():
		contact = shapecast.get_collision_point(0)
		normal = shapecast.get_collision_normal(0)
	else:
		contact = get_collision_point()
		normal = get_collision_normal()
	## Rotates wheel visuals
	var forward_dir := global_basis.z.normalized()
	var speed         := forward_dir.dot(car.linear_velocity)
	Wheel_model.rotate_x( (-speed * get_physics_process_delta_time()) / wheel_radius)
	
	
	contact = get_collision_point()
	var spring_len := maxf(0.0, global_position.distance_to(contact) - wheel_radius)
	var offset     := rest_dist - spring_len
	
	Wheel_model.position.y = move_toward(Wheel_model.position.y, -spring_len, 5 * get_physics_process_delta_time())
	contact = Wheel_model.global_position
	var force_pos     :Vector3= contact - car.global_position
	
	## Spring forces
	var spring_force  := spring_strenght * offset
	var tire_vel      := car._get_point_velocity(contact) #Center of the wheel
	var spring_damp_f := spring_damping * global_basis.y.dot(tire_vel)
	var suspension_force := clampf(spring_force - spring_damp_f, -max_spring_force, max_spring_force)
	
	var y_force       := suspension_force * get_collision_normal()
	
	## Acceleration
	if is_motor and car.motor_input:
		var speed_ratio: float= speed / car.max_speed
		var ac:          float= car.accel_curve.sample_baked(speed_ratio)
		var accel_force: Vector3= forward_dir * car.acceleration * car.motor_input * ac
		car.apply_force(accel_force, force_pos)
		#if show_debug: DebugDraw.draw_arrow_ray(contact, accel_force/car.mass, 2.5, 0.5, Color.RED)

	## Tire X traction (Steering)
	var steering_x_vel := global_basis.x.dot(tire_vel)
	
	grip_factor         = absf(steering_x_vel/tire_vel.length())
	if absf(speed) < 0.2:
		grip_factor = 0.0
	var x_traction     := grip_curve.sample_baked(grip_factor)
	if not car.hand_break:
		x_traction = 1
	elif car.is_slipping:
		x_traction = 0.1
	
	var gravity     := -car.get_gravity().y
	var x_force     := -global_basis.x * steering_x_vel * x_traction * ((car.mass * gravity)/car.total_wheels)
	
	
	## Tire z traction
	var f_speed     := forward_dir.dot(tire_vel)
	var z_friction  := z_traction
	if abs(f_speed) < 0.01:
		z_friction = 2.0
	if is_braking:
		z_friction = z_brake_traction
	var z_force     := global_basis.z * f_speed * z_friction * ((car.mass * gravity)/car.total_wheels)
	
	
	## Counter sliding
	if absf(f_speed) < 0.1:
		var susp := global_basis.y * suspension_force
		z_force.z -= susp.z *car.global_basis.y.dot(Vector3.UP)
		z_force.x -= susp.x *car.global_basis.y.dot(Vector3.UP)		
	
	car.apply_force(y_force, force_pos)
	car.apply_force(x_force, force_pos)
	car.apply_force(z_force, force_pos)
